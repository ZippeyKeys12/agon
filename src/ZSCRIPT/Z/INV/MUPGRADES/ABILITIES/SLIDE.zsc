extend
class ZMAU_PoweredSlide{
    [Config]{
        "SlideCost": "ABILITIES.iSlideCost",
        "SlidePower": "ABILITIES.iSlidePower",
        "SlideLength": "ABILITIES.iSlideLength",
        "SlideCooldown": "ABILITIES.iSlideCooldown",
        "SlideFactor": "ABILITIES.dSlideHeightFactor"
    }

    bool Sliding;

    int SlideTics,
        SlideCountdown;

    Vector2 Direction;

    override
    void Defaults(){
        Direction=(0, 0);
    }

    override
    void Tick(ZPlayer Player, PlayerInfo PlayerInfo){
        ZMarine Marine=ZMarine(Player);
        if(!Marine)return;
        if(Sliding){
            SlideTics--;
            PlayerInfo.CrouchFactor=SlideFactor;
            PlayerInfo.ViewHeight=Marine.ViewHeight*SlideFactor;
            Marine.LastVel.XY=Marine.Vel.XY;
            Marine.MStats.Energy-=SlideCost/SlideLength;
            Marine.LastVel.XY=Marine.Vel.XY=SlidePower*Direction;
            Marine.A_SetSize(NewHeight: Marine.FullHeight*SlideFactor);
            if((~Marine.Buttons & BT_CROUCH) || SlideTics==0){
                PlayerInfo.CrouchFactor=.51;
                SlideCountdown=SlideCooldown;
                Marine.LockMovement=Sliding=SlideTics=false;
                Marine.A_SetSize(NewHeight: Marine.FullHeight);
            }
        }else if(SlideCountdown>0)
            SlideCountdown--;
    }

    override
    void HandleMovement(ZPlayer Player, PlayerInfo PlayerInfo){
        ZMarine Marine=ZMarine(Player);
        if(!Marine)return;
        if(!Sliding && !SlideCountdown && PlayerInfo.OnGround && (Marine.SameButtons & BT_SPEED) && (Marine.NewButtons & BT_CROUCH) && Marine.MStats.Energy>SlideCost){
            Marine.LockMovement=Marine.LockTilt=Sliding=true;
            Direction=Marine.FrontVector.Unit();
            SlideTics=SlideLength;
        }
    }
}