class ZMarine:ZPlayer{
    [Property]{
        [Player]
        // Descriptors
        DisplayName="Marine";
        AttackZOffset=13;
        // Start Items
        StartItem="ZWeaponSystem";
        StartItem="Fist";
        StartItem=("Clip", 50);
        // Weapons
        WeaponSlot=(1, "Fist");
        WeaponSlot=(2, "ZWeaponSystem");
    }

    override
    void PreTick(){
        super.PreTick();
        MStats.ArmorTick(self);
    }

    override
    void UpdateStats(){
        [Health, MaxHealth, RunHealth]=MStats.GetHealth();
        A_SetHealth(Health);
        [ForwardMove1, ForwardMove2]=MStats.GetForwardMove();
        [SideMove1, SideMove2]=MStats.GetSideMove();
        ViewBob=MStats.ViewBob;
        JumpZ=MStats.JumpZ;
    }

    override
    double ShakeLevel() const
    {return MStats.ShakeLevel;}

    override
    int DamageMobj(Actor Inflictor, Actor Source, int Damage, Name Mod, int Flags, double Angle){
        if(Damage>0)
            MStats.Recharger=0;
        Damage=MStats.ModifyDamage(Inflictor, Source, Damage, Mod, true, Flags);
        Flags|=DMG_NO_ARMOR | DMG_NO_FACTOR;
        Damage=super.DamageMobj(Inflictor, Source, Damage, Mod, Flags, Angle);
        return Damage;
    }

    override
    int TakeSpecialDamage(Actor Inflictor, Actor Source, int Damage, Name DamageType){
        if(Damage>=TELEFRAG_DAMAGE)
            return Damage;
        MStats.Health-=Damage;
        return Damage;
    }
}

#include "ZSCRIPT/Z/PLYR/MARINE/STATS.zsc"
#include "ZSCRIPT/Z/PLYR/MARINE/MOVEMENT.zsc"